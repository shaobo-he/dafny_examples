
method {:axiom} random(a: int, b: int) returns (r: int)
  //  requires a <= b
  // Bounded nondeterministic choice only. This axiom does not specify a
  // probability distribution, independence between calls, or uniformity.
  ensures a <= b ==> a <= r <= b

lemma eqMultiset_t<T>(t: T, s1: seq<T>, s2: seq<T>)
  requires multiset(s1) == multiset(s2)
  ensures t in s1 <==> t in s2
{
  calc <==> {
    t in s1;
    t in multiset(s1);
    // Not necessary:
    //    t in multiset(s2);
    //    t in s2;
  }
  /*  
    if (t in s1) {
      assert t in multiset(s1);
    }
    else {
      assert t !in multiset(s1);
    }
  */
}

lemma eqMultiset<T>(s1: seq<T>, s2: seq<T>)
  requires multiset(s1) == multiset(s2)
  ensures forall t :: t in s1 <==> t in s2
{
  forall t {
    eqMultiset_t(t, s1, s2);
  }
}

method swap<T>(a: array<T>, i: int, j: int)
  // requires a != null
  requires 0 <= i < a.Length && 0 <= j < a.Length
  modifies a
  ensures a[i] == old(a[j])
  ensures a[j] == old(a[i])
  ensures forall m :: 0 <= m < a.Length && m != i && m != j ==> a[m] == old(a[m])
  ensures multiset(a[..]) == old(multiset(a[..]))
{
  var t := a[i];
  a[i] := a[j];
  a[j] := t;
}

method getAllShuffledDataEntries<T(0)>(m_dataEntries: array<T>) returns (result: array<T>)
  // Historical "shuffled" name: the verified result is a permutation of the
  // input. No uniformity or probability distribution is specified.
  // requires m_dataEntries != null
  // ensures result != null
  ensures fresh(result)
  ensures result.Length == m_dataEntries.Length
  ensures multiset(result[..]) == old(multiset(m_dataEntries[..]))
{
  result := new T[m_dataEntries.Length];
  forall i | 0 <= i < m_dataEntries.Length {
    result[i] := m_dataEntries[i];
  }

  assert result[..] == m_dataEntries[..];
  assert m_dataEntries[..] == old(m_dataEntries[..]);
  assert multiset(result[..]) == old(multiset(m_dataEntries[..]));

  var k := result.Length - 1;
  while (k >= 0)
    invariant multiset(result[..]) == multiset(m_dataEntries[..])
    invariant multiset(result[..]) == old(multiset(m_dataEntries[..]))
  {
    var i := random(0, k);
    assert i >= 0 && i <= k;

    if (i != k) {
      swap(result, i, k);
    }

    k := k - 1;
  }
}

function set_of_seq<T>(s: seq<T>): set<T>
{
  set x: T | x in s :: x
}

lemma {:axiom} in_set_of_seq<T>(x: T, s: seq<T>)
  ensures x in s <==> x in set_of_seq(s)

lemma {:axiom} subset_set_of_seq<T>(s1: seq<T>, s2: seq<T>)
  requires set_of_seq(s1) <= set_of_seq(s2)
  ensures forall x :: x in s1 ==> x in s2

method getRandomDataEntry<T(==)>(m_workList: array<T>, avoidSet: seq<T>) returns (e: T)
  // Verified as finding some entry outside avoidSet. This simple version is
  // deterministic linear search despite the historical method name.
  requires m_workList.Length > 0
  requires exists i :: 0 <= i < m_workList.Length && m_workList[i] !in avoidSet
  ensures e in old(m_workList[..]) && e !in avoidSet
{
  var k := 0;
  while k < m_workList.Length
    invariant 0 <= k <= m_workList.Length
    invariant forall i :: 0 <= i < k ==> m_workList[i] in avoidSet
  {
    e := m_workList[k];
    if e !in avoidSet {
      return e;
    }
    k := k + 1;
  }

  var i :| 0 <= i < m_workList.Length && m_workList[i] !in avoidSet;
  assert i < k;
  assert false;
}
